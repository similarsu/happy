plugins {
    id 'java'
    id 'net.ltgt.apt' version '0.10'
    id 'io.franzbecker.gradle-lombok' version '1.14'
}

group 'net.similarsu'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.4'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    apt 'org.projectlombok:lombok:1.18.4'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
}


import io.franzbecker.gradle.lombok.task.DelombokTask

lombok {
    version = '1.18.4'
    sha256 = '39f3922deb679b1852af519eb227157ef2dd0a21eec3542c8ce1b45f2df39742'
}

task delombok(type: DelombokTask, dependsOn: compileJava) {
    group = 'lombok'
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

task delombokHelp(type: DelombokTask) {
    group = 'lombok'
    args "--help"
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = false
    options{
        docEncoding 'utf-8'
        charSet 'utf-8'
    }
}